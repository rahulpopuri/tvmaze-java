plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group 'ca.bubblewrapstudios'
version '0.1'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'java-library'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'tvmaze-java'
        name = 'ca.bubblewrapstudios.tvmaze'
        userOrg = 'bubblewrapstudios'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/rahulpopuri/tvmaze-java.git'
        version {
            name = '1.0-SNAPSHOT'
            released = new Date()
        }
    }
    publications = ['MyPublication']
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'ca.bubblewrapstudios'
            artifactId 'tvmaze'
            version '0.1'
        }
    }
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Tv Maze Java API',
                'Implementation-Version': version,
                'Main-Class': 'ca.bubblewrapstudios.tvmaze.TvMaze'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.3.0'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.4'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.14.0'

    compileOnly 'org.projectlombok:lombok:1.16.6'

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'

}
